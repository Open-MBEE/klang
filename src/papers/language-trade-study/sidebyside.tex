
\section{The Shapes Example in OCLInEcore and K}
\label{sec:shapessidebyside}

In this section we show the shapes example in K and then in OCLInEcore (created by Maged Elaasar). The purpose is to give the
reader a quick survey of the differences between the two formalisms.

\lstset{language=K,numbers=none}

\begin{center}
\begin{tabular}{c}
\small
\begin{lstlisting}
package Shapes

class Shape {
  sides : Int
  fun area : Real
}

class Angle {
  value : Int

  fun eq(other: Angle) : Bool {
    value = other.value
  }

  req value >= 0 && value <= 360
}

class TAngle extends Angle {
  req value < 180
}

class Triangle extends Shape {

  a : TAngle
  b : TAngle
  c : TAngle

  base : Int
  height : Int
  
  req sides = 3

  fun area : Real {
    base * height / 2  
  }
  
  req Angles: a.value + b.value + c.value = 180
}

class  Equilateral extends Triangle {
  req a.eq(b) && b.eq(c) 
}

class Obtuse extends Triangle {
  req a.value > 90 || b.value > 90 || c.value > 90
}
\end{lstlisting}
\end{tabular}
\end{center}


\lstset{language=K,numbers=none}

\begin{center}
\begin{tabular}{c}
\small
\begin{lstlisting}
package Shapes : shapes = 'http://jpl.nasa.gov/shapes/'
{

  class Shape {
    attribute sides : Integer;
    operation area() : Real;
  }

  class Angle {
    attribute value : Integer;
    
    operation eq(other: Angle) : Boolean {
      body: value = other.value;
    }

    invariant: value >= 0 and value <= 360;
  }

  class TAngle extends Angle {
    invariant: value < 180;
  }

  class Triangle extends Shape {
    property a : TAngle;
    property b : TAngle;
    property c : TAngle;

    attribute base : Integer;
    attribute height : Integer;
 
    invariant: sides = 3;

    operation area() : Real {
      body: base * height / 2;
    }
 
    invariant Angles: a.value + b.value + c.value = 180;
  }

  class  Equilateral extends Triangle {
    invariant: a.eq(b) and b.eq(c);
  }

  class Obtuse extends Triangle {
    invariant: a.value > 90 or b.value > 90 or c.value > 90;
  }

}
\end{lstlisting}
\end{tabular}
\end{center}