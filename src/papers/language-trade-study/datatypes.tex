
\section{Modeling in K with data types}
\label{sec:datatypes}

Let us start with a very simple model. A spacecraft is a composition of hardware and software. To model this, we define three classes, named \name{Hardware}, \name{Software}, and 
\name{Spacecraft}:

\sk
\begin{lstlisting}
  class Hardware
  class Software

  class Spacecraft {
    var hardware : Hardware
    var software : Software
  }
\end{lstlisting}

\noindent
A \term{class} has a name and may contain a collection
of definitions. In the case of the classes \name{Hardware} and \name{Software}, there are no such definitions, corresponding to 
making an abstraction: we say no more about these concepts at this point. A \name{Spacecraft} on the other hands consists of two so-called
member declarations: a \name{hardware} field of type \name{Hardware},
and a \name{software} field of type \name{Software}.
These are variables that can be assigned values of their respective types.

\subsection{Refining the Hardware}

Before we refine\footnote{When we say we refine a class it means editing the original definition by 
adding more details. One can imagine support for a more formal approach where the old definition remains unchanged.} the
\name{Hardware} class, let's define a small collection of auxiliary concepts, such as a battery providing power, a power consuming unit,
an instrument, etc. 

A battery has a maximal charge and a current charge, and can
be recharged, as well as used, using the \name{update} function, which as argument takes the delta with which the battery should be updated (a negative number of power is used and a positive number
if power is added).

\sk
\begin{lstlisting}
  class Battery {
    val maxCharge : Real
    var currentCharge : Real

    fun update(DELTA: Real) 
      pre currentCharge + DELTA isin {0..maxCharge}
      post currentCharge isin {0..maxCharge}
    {
      currentCharge := currentCharge + delta;
    }
  }
\end{lstlisting}

\noindent
The \name{update} function is defined with a body, as well as 
a pre and post condition. The semantics of a pre-condition is
that it must be true before the function is applied, otherwise
the function result is ill-defined. The semantics of the 
post-condition is, that it is guaranteed to hold after the
function has been applied.

A power consuming device is of the following type:

\sk
\begin{lstlisting}
  class PowerConsumer {
    var currentPower : Real
  }
\end{lstlisting}

\noindent
Here a variable represents the current power consumption. We can 
now define some power consuming devices, such as the radio
and an instrument:

\sk
\begin{lstlisting}
class Radio extends PowerConsumer

class Instrument extends PowerConsumer {
  name : String
  weight : Real
  power : Real
  operating : Bool

  fun toggle() {
    operating := !operating;
    if !operating then
      power := 0
    end
  }
}
\end{lstlisting}

\noindent
An instrument has a name, a weight, a current power consumption,
and a flag indicating whether it is turned on or not, which
can be toggled with the \name{toggle} function.

Finally we can define the \name{Hardware} class:

\sk
\begin{lstlisting}
class Hardware {
  type WheelNo = {| n : Int . 1 <= n and n <= 6 |}

  var wheels : Map[1..6,Wheel]
  var instruments : Set[Instrument]
  var battery : Battery

  val maxWeight : Real

  req instrumentsUnique :
    forall i1,i2 : instruments .
      i1 != i2 => i1.name != i2.name

  req notTooHeavy:
    instruments.collect(i -> i.weight).sum() <= maxWeight;

  req controlPowerUsage :
    instruments.collect(i -> i.currentPower).sum() <= battery.remaining * 0.5
 }
\end{lstlisting}

\noindent
We first define a type \name{WheelNo} of wheel numbers, namely the
numbers 1 to 6. \name{WheelNo} is a predicate subtype of the type \name{Int} of integers. 

The class then contains four variables.
%
The variable \name{wheels} is a finite mapping from wheel numbers to wheels. A mapping is essentially a function, 
but which is only defined on a finite domain, and which
can be updated.
%
The variable \name{instruments} denotes a set of instruments.
The type \code{Set[Int]} is the type of all sets of integers. Each element of this type is a set of integers.
%

\subsection{Refining the Software}

The software in turn consists of flight software, which
runs on the spacecraft, and ground software, which runs
on grounding, sending commands to the spacecraft and receiving telemetry from the spacecraft:

\sk
\begin{lstlisting}
class Softare {
  flight : FlightSoftware
  ground : GroundSoftware
}
\end{lstlisting}

\noindent
All software shares common attributes, the \name{SoftwareBasis}:

\sk
\begin{lstlisting}
class SoftwareBasis {
  type ModuleName = String
  type Language = String

  val usedLanguages : Set[Language] = {"Java","C","C++"}

  modules : Map[ModuleName,Module]

  codingStandards : Map[Language,List[Rule]]

  fun codingStandardCheck : Module -> List[Int * Int]

  fun linesOfCode() : Int = modules.range().collect(m -> m.code.length()).sum()

  req codeCorrect : forall module : modules.range() . standardChecker(module).isEmpty();
}
\end{lstlisting}

\noindent
We first define the type of \name{ModuleName} 
module names, which are basically just strings. Such
type definitions are just aliases. Hence in the scope
of this definition the type \name{ModuleName} is equivalent to the type \name{Int}.

\sk
\begin{lstlisting}
class Module {
  language : ProgrammingLanguage
  code : List[String]
}
\end{lstlisting}

\sk
\begin{lstlisting}
enum ProgrammingLanguage{Java,C,Cpp}
\end{lstlisting}

\sk
\begin{lstlisting}
class FlightSoftware extends SoftwareBasis {

}
\end{lstlisting}

\sk
\begin{lstlisting}
class GroundSoftware extends SoftwareBasis {
  logs : List[Log]

  req logsSortedWrtTime :
    forall (log1,log2) : logs.pairs() . log1.startTime <= log2.startTime

  fun find(startTime: Int, endTime: Int): List[Event] =
    logs.select(log -> log.startTime isin startTime .. endTime).flatten()
}
\end{lstlisting}

\sk
\begin{lstlisting}
class Log {
  events : List[Event]

  req eventsSortedWrtTime :
   forall (e1,e2) : events.pairs() . e1.time <= e2.timex

  req eventsWellformed :
    events.forall(e -> e.wellformed())
}
\end{lstlisting}

\sk
\begin{lstlisting}
class LogEntry {
  fun wellformed: Unit -> Bool
}
class Event extends LogEntry {
  kind : String
  fields : Map[String,String]
  fun wellformed() = schema(kind) subsetof fields
}
class Sampling extends LogEntry {
  sensor : String
  value : Real

  fun wellformed() = true
}
\end{lstlisting}


\noindent
